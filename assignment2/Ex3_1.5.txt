// Authors : Denis Hoornaert (000413326), Nathan Liccardo (000408278) and Remy Detobel (000408013)
// Objective: reg1 * reg2 = reg4(MSB) reg3(LSB)

// Memory organisation:
// +-----------+-------------------------------------+
// | addr      | content                             |
// +-----------+-------------------------------------+
// | 0x01  [1] | operand 1                           |
// | 0x02  [2] | operand 2                           |
// | 0x03  [3] | result LSB                          |
// | 0x04  [4] | result MSB                          |
// | 0x05  [5] | counter                             |
// | 0x06  [6] | MSB of the operand 1 (used for sum) |
// +-----------+-------------------------------------+


            // Init entries
            movi 1, 0xffff                                        // Operand 1 LSB (used as base for every addition)
            movi 2, 0x0007                                        // Operand 2 LSB (used for comparison with counter)
            // nop
            // nop
            // nop
            // nop

// PART 1 :
//  Test whether at least one operand is equal to 0.
//  If yes, we know for sure that the result is then 0. Otherwise, the program resumes.
            movi 3, 0x0000                                        // Null value
            beq 1, 3, zerodetected
            beq 2, 3, zerodetected

            movi 7, <? endcond ?>                                 // Else : resume
            jalr 7, 7

zerodetected: movi 4, 0x0000                                      // Result MSB
            halt                                                  // If : halt

// PART 2 :
//  Test whether operand 1 is lower than operand 2. If yes, resume in init (do nothing).
//  Otherwise, swap the registers content so that the counter containing the lowest value
//  is used as the counter. The expected result is to mitigate the number of loop entering.
endcond:    bl 2, 1, init                                         // If op2 < op1 -> jump to init
            // Else: inverse reg1 and reg2
            add 7, 1, 3                                           // Save reg1 in reg7
            add 1, 2, 3                                           // reg2 in reg1
            add 2, 7, 3                                           // save of reg1 in reg2

// PART 3 :
//  Simple initialization of register 5 and 6. Thereafter, the context of the loop is saved
//  before jumping to the loop part (preloop is skipped on purpose has no precomputation can be loaded).
init:       movi 5, 0x0000                                        // Result LSB
            movi 6, 0x0000                                        // Result MSB
            add 7, 3, 2                                           // Counter = op2 (& reg3 = 0)

            // Save context                                       // Memory
            sw 1, 3, 0x0001                                       // 1.  operand 1 (add on every loop)
            sw 2, 3, 0x0002                                       // 2.  operand 2 (used to check whether x2 can be saved)
            sw 5, 3, 0x0003                                       // 3.  result LSB
            sw 6, 3, 0x0004                                       // 4.  result MSB

            movi 1, 0x0000
            movi 2, <? loop ?>
            jalr 2, 2

// PART 4 :
//  Decrement the counter and test whether it is the end of the loop (i.e. counter is equal to 0). If yes, the program halts.
preloop:    addi 5, 1, 1                                          // movi 5, 0x0001 in ONE instruction
            sub 7, 7, 5                                           // Decrement of the counter

            beq 7, 1, end
            movi 5, <? loop ?>
            jalr 5, 5

end:        lw 3, 1, 0x0003                                       // Load result
            lw 4, 1, 0x0004
            halt                                                  // Halt program


            // Here we have:
            // 1: memory start address
            // 2: mask
            // 3: op1
            // 4: op1 (MSB)
            // 5: use for comparison
            // 6: use for comparison
            // 7: counter

// PART 5 :
//  This portion of the code starts with four initialisations and is followed by a loop ('testBig') which performs each time a shift on the mask.
//  The aim of the loop is to find the first bit set to '1' in the register (from left to right).
//  The result of the shift is at each loop entering compared to both the counter dedicated to the shifts and the counter dedicated to the
//  multiplication. In the latter case, we check the equality (the program jumps in the addition is so) while in the former case, the program checks
//  whether the multiplication counter is bigger than the shifted mask (if yes : the program resumes in 'bigger').
loop:       movi 2, 0x000a                                        // Counter number of shift
            movi 6, 0x03ff                                        // Mask to test whether a number is bigger than the counter

            lw 3, 1, 0x0001                                       // LSB for operand 1 x1
            movi 4, 0x0000                                        // MSB for operand 1 x1

testBig:    beq 1, 2, addOp
            bg 7, 6, bigger
            addi 5, 1, 1                                          // movi 5, 0x0001 in ONE instruction :)
            sub 2, 2, 5
            shifti 6, 6, -1
            movi 5, <? testBig ?>
            jalr 5, 5

//  Subtraction of the highest possible value reachable by shifting
bigger:     sub 7, 7, 6

// PART 6 :
//  Simple shift to the left on 32 bits as long as register 2 (size of mask memory) is different than register 1 (mask memory lower bound).
//  At the end, registers 3 and 4 contain the addresses needed to load the good intermediate results later in the code (in 'addOp').
            movi 6, 0x0001
nextShl:    beq 2, 1, addOp                                       // If we made all shift
            sub 2, 2, 6
            shl 4, 4, 6
            shl 3, 3, 6, shiftCarry
            movi 5, <? nextShl ?>
            jalr 5, 5

shiftCarry: addi 4, 4, 0x0001
            movi 5, <? nextShl ?>
            jalr 5, 5


// PART 7 :
//  Simple addition with carry and then jump back to 'preloop'.
//  In other words :
//      reg4 reg3 = reg4 reg3 + reg6 reg5
//      result    = operand 1 (or x2, x4, x9, ..., x1024) + result

addOp:      lw 5, 1, 0x0003                                       // Load LSB result in reg 5
            lw 6, 1, 0x0004                                       // Load MSB of operand 2

            add 4, 4, 6
            add 3, 3, 5, carr_inc
            movi 5, <? aft_ex ?>                                  // aft_ex addr
            jalr 5, 5

carr_inc:   addi 4, 4, 0x0001

aft_ex:     sw 3, 1, 0x0003                                       // Save result (LSB)
            sw 4, 1, 0x0004                                       // Save result (MSB)

            movi 6, <? preloop ?>                                 // preloop flag addr
            jalr 6, 6
